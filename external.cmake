include(ExternalProject)
set(semicolon_smuggle "-+-")
string(REPLACE ";" "${semicolon_smuggle}" CMAKE_OSX_ARCHITECTURES "${CMAKE_OSX_ARCHITECTURES}")


ExternalProject_Add(snappy
  PREFIX ${EXTERNAL_DIR}
  SOURCE_DIR ${EXTERNAL_DIR}/snappy
  INSTALL_DIR ${USR_DIR}
  GIT_REPOSITORY https://github.com/google/snappy.git
  GIT_TAG 1.2.2
  GIT_SHALLOW TRUE
  LIST_SEPARATOR ${semicolon_smuggle}
  CMAKE_ARGS
    -DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DSNAPPY_BUILD_BENCHMARKS=OFF
    -DSNAPPY_BUILD_TESTS=OFF
    -DCMAKE_INSTALL_PREFIX:PATH=${USR_DIR}
  DOWNLOAD_EXTRACT_TIMESTAMP TRUE
)


if(${MADS_ENABLE_LOGGER})
  ExternalProject_Add(mongocxx
    PREFIX ${EXTERNAL_DIR}
    SOURCE_DIR ${EXTERNAL_DIR}/mongocxx
    INSTALL_DIR ${USR_DIR}
    GIT_REPOSITORY https://github.com/mongodb/mongo-cxx-driver.git
    GIT_TAG r4.0.0
    GIT_SHALLOW TRUE
    LIST_SEPARATOR ${semicolon_smuggle}
    CMAKE_ARGS 
      -DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
      -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
      -DENABLE_SNAPPY=OFF
      -DCMAKE_CXX_STANDARD=17
      -DENABLE_STATIC=ON
      -DENABLE_SHARED=ON
      -DBUILD_SHARED_AND_STATIC_LIBS=ON
      -DENABLE_TESTS=OFF
      -DENABLE_ZSTD=OFF
      -DENABLE_SSL=OFF
      -DMONGOCXX_ENABLE_SSL=$<IF:$<BOOL:${WIN32}>,OFF,AUTO> 
      -DENABLE_EXAMPLES=OFF
      -DCMAKE_INSTALL_PREFIX:PATH=${USR_DIR}
      -DENABLE_MONGODB_AWS_AUTH=${MADS_ENABLE_MONGODB_AWS}
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
  )
  add_dependencies(mongocxx snappy)
endif()
